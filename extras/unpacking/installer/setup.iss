; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Indetectables Toolkit Extras: Unpacking"
#define MyAppNameOriginal "Indetectables Toolkit"
#define MyAppVersion "2023.11"
#define MyAppPublisher "Indetectables"
#define MyAppURL "https://www.indetectables.net/"
#define MyAppToolsFolder "{app}\toolkit"
#define MyAppBinsFolder "{app}\bin"
#define MySrcDir "C:\code\toolkit\extras\unpacking"
#define MyOutputDir "C:\code"

[Setup]
AppId={{968CE526-6B32-4998-84F9-6CDD43CFBC2C}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
VersionInfoVersion={#MyAppVersion}
AppVerName={#MyAppName} - {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={sd}\{#MyAppNameOriginal}
DefaultGroupName={#MyAppNameOriginal}
PrivilegesRequired=admin
;PrivilegesRequiredOverridesAllowed=dialog
OutputBaseFilename=Toolkit_{#MyAppVersion}_Extras_Unpacking
Compression=lzma2/max
SolidCompression=yes
WizardStyle=modern
ArchitecturesInstallIn64BitMode=x64
SetupIconFile="{#MySrcDir}\installer\icon.ico"
OutputDir={#MyOutputDir}
Uninstallable=no
DirExistsWarning=no

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "spanish"; MessagesFile: "compiler:Languages\Spanish.isl"
Name: "catalan"; MessagesFile: "compiler:Languages\Catalan.isl"
Name: "portuguese"; MessagesFile: "compiler:Languages\BrazilianPortuguese.isl"

[CustomMessages]
english.FolderValidationError=The toolkit was not found in the selected folder. You must install it before you can continue.
spanish.FolderValidationError=No se ha encontrado el kit de herramientas en la carpeta seleccionada. Debe instalarlo antes de continuar.
catalan.FolderValidationError=El conjunt d'eines no s'ha trobat a la carpeta seleccionada. Heu d'instal·lar-lo abans de poder continuar.
portuguese.FolderValidationError=O conjunto de ferramentas não foi encontrado na pasta seleccionada. Deve ser instalado antes de poder continuar.

[Components]
Name: "unpacking"; Description: "UnPacking"; Types: full;
#include "sections\unpacking.iss"

[Components]
Name: "updater"; Description: "Tools auto updater"; Types: full;
#include "sections\updater.iss"


;;; etc
[Run]
; Configure installed tools
Filename: "{#MyAppBinsFolder}\updater\bin\auto-config-ini.exe"; Parameters: "/FOLDER={#MyAppBinsFolder}\updater /TYPE=clean"; Flags: runhidden;

; Sync tools versions
Filename: "{#MyAppBinsFolder}\updater\bin\auto-config-ini.exe"; Parameters: "/FOLDER={#MyAppBinsFolder}\updater /TYPE=sync"; Flags: runhidden;

[code]
function GetFileName(const FileName: string): string;
begin
    Result := ExpandConstant('{#MyAppBinsFolder}\updater\' + FileName);
end;

procedure BeforeInstallScript();
begin
    if FileExists(GetFileName('tools.ini')) then 
    begin
        RenameFile(GetFileName('tools.ini'), GetFileName('tools.ini.old'));
    end;
end;

function NextButtonClick(PageId: Integer): Boolean;
begin
    Result := True;
    if (PageId = wpSelectDir) and not FileExists(ExpandConstant('{app}\CHANGELOG.md')) then
    begin
        MsgBox(ExpandConstant('{cm:FolderValidationError}'), mbError, MB_OK);
        Result := False;
        exit;
    end;
end;

procedure CurStepChanged(CurStep: TSetupStep);
begin
    if (CurStep = ssDone) and FileExists(GetFileName('tools.ini.old')) then 
    begin
        DeleteFile(GetFileName('tools.ini.old'));
    end;
end;
